plugins {
   id("us.ihmc.ihmc-build") version "0.19.5"
   id("us.ihmc.ihmc-ci") version "4.25"
   id("us.ihmc.ihmc-cd") version "0.1"
   id("us.ihmc.log-tools") version "0.3.1"
   id("org.hidetake.ssh") version "2.1.1"
}

import org.gradle.api.GradleException

ihmc {
   //loadProductProperties("../product.properties")

   configureDependencyResolution()
   configurePublications()
}

mainDependencies {
   compile("us.ihmc:euclid-frame:0.12.0")
   compile("us.ihmc:euclid-shape:0.12.0")
   compile("us.ihmc:ihmc-yovariables:0.3.11")
   compile "org.ejml:dense64:0.30"
   compile "org.ejml:core:0.30"
   compile "net.sf.trove4j:trove4j:3.0.3"
   compile "com.thoughtworks.xstream:xstream:1.4.7"
   compile("us.ihmc:ihmc-communication:source")
   compile("us.ihmc:ihmc-common-walking-control-modules:source")
   compile("us.ihmc:ihmc-robot-models:source")
   compile("us.ihmc:ihmc-sensor-processing:source")
   compile("us.ihmc:ihmc-humanoid-robotics:source")
   compile("us.ihmc:ihmc-humanoid-behaviors:source")
   compile("us.ihmc:ihmc-avatar-interfaces:source")
   compile("us.ihmc:simulation-construction-set:0.12.15")
   compile("us.ihmc:ihmc-state-estimation:source")
   compile("us.ihmc:atlas:source")
   compile("us.ihmc:ihmc-java-toolkit:source")
   compile("us.ihmc:ihmc-commons:0.26.6")
   compile("us.ihmc:ihmc-robotics-toolkit:source")
   compile("us.ihmc:ihmc-graphics-description:0.12.12")
   compile("us.ihmc:ihmc-geometry:0.12.5")
   compile("us.ihmc:ihmc-simulation-toolkit:source")
}

if (ihmc.isBuildRoot())
{
   remotes {
      testbed {
         host = '10.7.4.50'

         // Set the username and password in ~/.gradle/gradle.properties.
         user = project.hasProperty('testbed_username') ? "${testbed_username}" : "invalid"
         password = project.hasProperty('testbed_password') ? "${testbed_password}" : "invalid"

         knownHosts = allowAnyHosts
      }
   }

   task deploy(dependsOn: ['jar']) {
      ihmc.jarWithLibFolder()
      def projectDir = project.buildscript.sourceFile.parent
      def directory = 'testbed'

      doLast {
         if (!project.hasProperty('testbed_username') || !project.hasProperty('testbed_password'))
         {
            throw new GradleException("Please set testbed_username and testbed_password in ~/.gradle/gradle.properties. See https://docs.gradle.org/current/userguide/build_environment.html#sec:gradle_properties_and_system_properties for more information.")
         }

         ssh.run {
            session(remotes.testbed) {
               execute("mkdir -p " + directory + "/lib")

               configurations.runtime.each {
                  put it, directory + '/lib'
               }

               put jar.archivePath, directory + '/testbed.jar'

               def scriptDir = new File(projectDir, "launchScripts")
               def scriptDirCollection = files { scriptDir.listFiles() }
               put scriptDirCollection, directory

               scriptDirCollection.each {
                  execute 'chmod 777 ' + directory + '/' + it.name
               }
            }
         }
      }
   }
}